Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Бухгалтерский.Ссылка
	|ИЗ
	|	ПланСчетов.Бухгалтерский КАК Бухгалтерский
	|ГДЕ
	|	Бухгалтерский.Валютный = ИСТИНА";

	РезультатЗапроса = Запрос.Выполнить();

	Блокировка = Новый БлокировкаДанных;

	ЧтоБлокируем = Блокировка.Добавить("РегистрБухгалтерии.Проводки");
	ЧтоБлокируем.ИсточникДанных = РезультатЗапроса;

	ЧтоБлокируем.ИспользоватьИзИсточникаДанных("Счет", "Ссылка");

	Блокировка.Заблокировать();
	
	
	

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПроводкиОстатки.Счет,
	|	ПроводкиОстатки.Субконто1,
	|	ПроводкиОстатки.Субконто2,
	|	ПроводкиОстатки.Подразделение,
	|	ПроводкиОстатки.Валюта,
	|	ПроводкиОстатки.СуммаОстатокДт,
	|	ПроводкиОстатки.СуммаОстатокКт,
	|	ПроводкиОстатки.ВалютнаяСуммаОстатокДт,
	|	ПроводкиОстатки.ВалютнаяСуммаОстатокКт
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	РегистрБухгалтерии.Проводки.Остатки(&Период, Счет.Валютный = ИСТИНА,, Организация = &Организация) КАК ПроводкиОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОстатки.Счет КАК Счет,
	|	ВТОстатки.Субконто1,
	|	ВТОстатки.Субконто2,
	|	ВТОстатки.Подразделение КАК Подразделение,
	|	ВТОстатки.Валюта КАК Валюта,
	|	ВТОстатки.СуммаОстатокДт,
	|	ВТОстатки.СуммаОстатокКт,
	|	ВТОстатки.ВалютнаяСуммаОстатокДт,
	|	ВТОстатки.ВалютнаяСуммаОстатокКт,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) КАК Кратность
	|ИЗ
	|	ВТОстатки КАК ВТОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период,) КАК КурсыВалютСрезПоследних
	|		ПО ВТОстатки.Валюта = КурсыВалютСрезПоследних.Валюта
	|ИТОГИ
	|	МАКСИМУМ(Курс) КАК Курс,
	|	МАКСИМУМ(Кратность) КАК Кратность
	|ПО
	|	Валюта,
	|	Счет";

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Дата);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаВалюта = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаВалюта.Следующий() Цикл

		Если (ВыборкаВалюта.Кратность = 0) Тогда
			КроссКурс = ВыборкаВалюта.Курс * 10000;
		Иначе
			КроссКурс = ВыборкаВалюта.Курс / ВыборкаВалюта.Кратность * 10000;
		КонецЕсли;

		ВыборкаСчет = ВыборкаВалюта.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаСчет.Следующий() Цикл

			ВыборкаДетальныеЗаписи = ВыборкаСчет.Выбрать();

			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

				КурсоваяРазница = ВыборкаДетальныеЗаписи.ВалютнаяСуммаОстатокДт * КроссКурс / 10000
					- ВыборкаДетальныеЗаписи.СуммаОстатокДт;
				
				//Если КурсоваяРазница = 0 то в если не зайдем.
				Если (КурсоваяРазница > 0) Тогда
					ДоходыОтКурсовойРазницы(КурсоваяРазница, ВыборкаДетальныеЗаписи.Счет,
						ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Валюта,
						ВыборкаДетальныеЗаписи.Субконто1, ВыборкаДетальныеЗаписи.Субконто2);
				ИначеЕсли (КурсоваяРазница < 0) Тогда

					РасходыОтКурсовойРазницы(-1 * КурсоваяРазница, ВыборкаДетальныеЗаписи.Счет,
						ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Валюта,
						ВыборкаДетальныеЗаписи.Субконто1, ВыборкаДетальныеЗаписи.Субконто2);

				КонецЕсли;

				КурсоваяРазница = ВыборкаДетальныеЗаписи.ВалютнаяСуммаОстатокКт * КроссКурс / 10000
					- ВыборкаДетальныеЗаписи.СуммаОстатокКт;
				
				//Если КурсоваяРазница = 0 то в если не зайдем.
				Если (КурсоваяРазница < 0) Тогда
					ДоходыОтКурсовойРазницы(-1 * КурсоваяРазница, ВыборкаДетальныеЗаписи.Счет,
						ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Валюта,
						ВыборкаДетальныеЗаписи.Субконто1, ВыборкаДетальныеЗаписи.Субконто2);
				ИначеЕсли (КурсоваяРазница > 0) Тогда

					РасходыОтКурсовойРазницы(КурсоваяРазница, ВыборкаДетальныеЗаписи.Счет,
						ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Валюта,
						ВыборкаДетальныеЗаписи.Субконто1, ВыборкаДетальныеЗаписи.Субконто2);

				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Движения.Проводки.Записывать = Истина;

КонецПроцедуры

Процедура ДоходыОтКурсовойРазницы(КурсоваяРазница, Счет, Подразделение, Валюта, Субконто1, Субконто2)

	Движение = Движения.Проводки.Добавить();

	Движение.СчетДт = Счет;
	Движение.ВалютаДт = Валюта;
	БухгалтерияСервер.УстановитьСубконто(Счет, Движение.СубконтоДт, 1, Субконто1);
	БухгалтерияСервер.УстановитьСубконто(Счет, Движение.СубконтоДт, 2, Субконто2);
	Движение.ВалютнаяСуммаДт = 0;

	Движение.СчетКт = ПланыСчетов.Бухгалтерский.Капитал;

	БухгалтерияСервер.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);

	Движение.Период = Дата;
	Движение.Организация = Организация;
	Движение.Сумма = КурсоваяРазница;
	Движение.Содержание = "Доходы от курсовой разницы";

КонецПроцедуры

Процедура РасходыОтКурсовойРазницы(КурсоваяРазница, Счет, Подразделение, Валюта, Субконто1, Субконто2)

	Движение = Движения.Проводки.Добавить();

	Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;

	Движение.СчетКт = Счет;
	Движение.ВалютаКт = Валюта;
	Движение.ВалютнаяСуммаКт = 0;
	БухгалтерияСервер.УстановитьСубконто(Счет, Движение.СубконтоКт, 1, Субконто1);
	БухгалтерияСервер.УстановитьСубконто(Счет, Движение.СубконтоКт, 2, Субконто2);

	БухгалтерияСервер.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);

	Движение.Период = Дата;
	Движение.Организация = Организация;
	Движение.Сумма = КурсоваяРазница;
	Движение.Содержание = "Расходы от курсовой разницы";

КонецПроцедуры