Процедура ЗаполнитьПодразделениеСтороныПроводки(Движение, Подразделение,
		ДтКт) Экспорт

	Если (ЗначениеЗаполнено(Движение["Счет" + ДтКт]) = Ложь) Тогда
		Возврат;
	КонецЕсли;

	ДанныеСчета = ОбщийМодульНаСервереВызовСКлиента.ПолучитьСвойствоСчета(Движение["Счет" + ДтКт]);

	
	
	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("ПФО_Организация", Движение.Организация);

	ПоПодразделениям = ПолучитьФункциональнуюОпцию("УчетПоПодразделениям", ПараметрыФО);

	
	
	Если ПоПодразделениям И ДанныеСчета.ПоПодразделениям Тогда

		Движение["Подразделение" + ДтКт] = Подразделение;

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСубконтоСтороныПроводки(Движение, ИмяИлиКоличествоСубконто, ЗначениеСубконто, ДтКт) Экспорт

	Если (ЗначениеЗаполнено(Движение["Счет" + ДтКт]) = Ложь) Тогда
		Возврат;
	КонецЕсли;

	ДанныеСчета = ОбщийМодульНаСервереВызовСКлиента.ПолучитьСвойствоСчета(Движение["Счет"
		+ ДтКт]);

	Если ТипЗнч(ИмяИлиКоличествоСубконто) = Тип("Строка") Тогда

		Для индекс = 1 По ДанныеСчета.КоличествоСубконто Цикл

			Если ДанныеСчета["ВидСубконто" + индекс + "ИмяПредопределенныхДанных"] = ИмяИлиКоличествоСубконто  Тогда

				Движение["Субконто" + ДтКт].Вставить(ДанныеСчета["ВидСубконто"
					+ индекс], ЗначениеСубконто);

				Прервать;
			КонецЕсли;

			Если ДанныеСчета["ВидСубконто" + индекс + "Наименование"] = ИмяИлиКоличествоСубконто Тогда

				Движение["Субконто" + ДтКт].Вставить(ДанныеСчета["ВидСубконто"
					+ индекс], ЗначениеСубконто);

				Прервать;
			КонецЕсли;

			Если ДанныеСчета["ВидСубконто" + индекс + "ТипЗначения"] = ТипЗнч(ЗначениеСубконто) Тогда

				Движение["Субконто" + ДтКт].Вставить(ДанныеСчета["ВидСубконто"
					+ индекс], ЗначениеСубконто);

				Прервать;
			КонецЕсли;

		КонецЦикла;

	ИначеЕсли ТипЗнч(ИмяИлиКоличествоСубконто) = Тип("Число") Тогда

		Если ИмяИлиКоличествоСубконто <= ДанныеСчета.КоличествоСубконто Тогда

			Движение["Субконто" + ДтКт].Вставить(ДанныеСчета["ВидСубконто"
				+ ИмяИлиКоличествоСубконто], ЗначениеСубконто);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьКоличествоСтороныПроводки(Движение, Количество,
		ДтКт) Экспорт

	Если (ЗначениеЗаполнено(Движение["Счет" + ДтКт]) = Ложь) Тогда
		Возврат;
	КонецЕсли;

	ДанныеСчета = ОбщийМодульНаСервереВызовСКлиента.ПолучитьСвойствоСчета(Движение["Счет"
		+ ДтКт]);

	Если ДанныеСчета.Количественный = Истина Тогда

		Движение["Количество" + ДтКт] = Количество;

	КонецЕсли;

КонецПроцедуры
// Процедура - Изменить активность движений
//
// Параметры:
//  Ссылка			 - ДокументСсылка	-  Может быть ссылка на любой документ
//  НужнаяАктивность - Булево	 -  По Умолчанию = Неопределено
//
Процедура ИзменитьАктивностьДвижений(Ссылка, НужнаяАктивность = Неопределено) Экспорт

	НЗ = РегистрыБухгалтерии.Проводки.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(Ссылка);

	НЗ.Прочитать();

	Если НужнаяАктивность = Неопределено Тогда

		Если НЗ.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		НужнаяАктивность = Не НЗ[0].Активность;

	КонецЕсли;

	НЗ.УстановитьАктивность(НужнаяАктивность);

	НЗ.Записать();

КонецПроцедуры

Процедура ПроверкаЗаполненияПодразделенияОбработкаПроверкиЗаполнения(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт

	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("ПФО_Организация", Источник.Организация);

	ПоПодразделениям = ПолучитьФункциональнуюОпцию("УчетПоПодразделениям", ПараметрыФО);

	Если ПоПодразделениям = Ложь Тогда
		
		Индекс =  ПроверяемыеРеквизиты.Найти("Подразделение");
		
		Если Индекс <> Неопределено Тогда
		
			ПроверяемыеРеквизиты.Удалить(Индекс);
		
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры
