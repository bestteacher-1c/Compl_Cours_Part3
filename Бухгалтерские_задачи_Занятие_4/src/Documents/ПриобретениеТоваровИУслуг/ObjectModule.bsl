

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Проводки.Записывать = Истина;
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		
		Движение = Движения.Проводки.Добавить();
		
		//--Общие для проводки -----
		
		Движение.Период = Дата;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.СодержаниеПроводки = "Поступили ТМЦ";
		Движение.Организация = Организация;
		
		//--Дебет -----
		Движение.СчетДт = ТекСтрокаТовары.СчетУчета;
		//Движение.ПодразделениеДт = Подразделение;
		ОбщийМодульНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение,Подразделение,"Дт");
		
		ОбщийМодульНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение,"Номенклатура", ТекСтрокаТовары.Номенклатура, "Дт");
		ОбщийМодульНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение,"Склады", Склад, "Дт");
		ОбщийМодульНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение,"Контрагенты", Контрагент, "Дт");
		
		//--Кредит ----
		Если Не ТекСтрокаТовары.СчетУчета.Забалансовый Тогда
		
			Движение.СчетКт = ПланыСчетов.Счета.Поставщики;
			ОбщийМодульНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение,Подразделение,"Кт");
			
			ОбщийМодульНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение,"Контрагенты", Контрагент, "Кт");
			ОбщийМодульНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение,"Договоры", Договор, "Кт");
		
		КонецЕсли; 
		
		
	КонецЦикла;

КонецПроцедуры




