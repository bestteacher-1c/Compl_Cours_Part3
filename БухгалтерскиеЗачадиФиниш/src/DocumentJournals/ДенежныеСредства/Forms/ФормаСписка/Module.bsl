
&НаСервере
Процедура ОбновитьТекущийОстатокНаСервере()

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроводкиОстатки.Счет,
	|	ПроводкиОстатки.СуммаОстатокДт
	|ИЗ
	|	РегистрБухгалтерии.Проводки.Остатки(&Дата, Счет В (&СчетКасса, &СчетБанк), ) КАК ПроводкиОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроводкиОбороты.Счет,
	|	ЕСТЬNULL(ПроводкиОбороты.СуммаОборотДт, 0) КАК СуммаОборотДт,
	|	ЕСТЬNULL(ПроводкиОбороты.СуммаОборотКт, 0) КАК СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Проводки.Обороты(&Дата1, &Дата2, , Счет В (&СчетКасса, &СчетБанк), , ) КАК ПроводкиОбороты";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("Дата1", ДобавитьМесяц(НачалоМесяца(ТекущаяДата()),-1));
	Запрос.УстановитьПараметр("Дата2", (НачалоМесяца(ТекущаяДата())-1));
	Запрос.УстановитьПараметр("СчетКасса", ПланыСчетов.Счета.Касса);
	Запрос.УстановитьПараметр("СчетБанк", ПланыСчетов.Счета.СчетВБанке);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультатов.Получить(0).Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Если ВыборкаДетальныеЗаписи.Счет = ПланыСчетов.Счета.Касса Тогда
			КассаОстаток = ВыборкаДетальныеЗаписи.СуммаОстатокДт;
		Иначе	
			БанкОстаток = ВыборкаДетальныеЗаписи.СуммаОстатокДт;
		КонецЕсли;

	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = МассивРезультатов.Получить(1).Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Если ВыборкаДетальныеЗаписи.Счет = ПланыСчетов.Счета.Касса Тогда
			КассаПриход = ВыборкаДетальныеЗаписи.СуммаОборотДт;
			КассаРасход = ВыборкаДетальныеЗаписи.СуммаОборотКт;
		Иначе	
			БанкПриход = ВыборкаДетальныеЗаписи.СуммаОборотДт;
			БанкРасход = ВыборкаДетальныеЗаписи.СуммаОборотКт;
		КонецЕсли;
		
	КонецЦикла;
	

	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущийОстаток(Команда)
	ОбновитьТекущийОстатокНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьТекущийОстатокНаСервере();
КонецПроцедуры


//--Подключение обработки ожидания---

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ПолучитьТекущийОстаток",60,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТекущийОстаток() Экспорт 

	ОбновитьТекущийОстатокНаСервере();

КонецПроцедуры // ПолучитьТекущийОстаток()

