Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Движения.Проводки.Записать();

	Если (Константы.УчетПоСкладам.Получить() = Истина) Тогда
		ОбработкаПроведенияУчетПоСкладам(Отказ, РежимПроведения);
	Иначе
		ОбработкаПроведенияНетУчетаПоСкладам(Отказ, РежимПроведения);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведенияУчетПоСкладам(Отказ, РежимПроведения)

	Если (Константы.СуммовойУчетПоСкладам.Получить() = Ложь) Тогда
		
		ОбработкаПроведенияНетУчетаПоСкладам(Отказ, РежимПроведения);
		//Корректировать себестоимость с учетом складов нет смысла
		
		Возврат;
		
	КонецЕсли;


	Движения.Проводки.Записывать = Истина;

	Запрос = Новый Запрос; 

	ВидыСубконто = Новый Массив; 
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);

	Запрос.Текст = "ВЫБРАТЬ
		|	ПроводкиОстаткиИОбороты.Субконто1 КАК Номенклатура,
		|	ПроводкиОстаткиИОбороты.Субконто2 КАК Склад,
		|	ПроводкиОстаткиИОбороты.Счет,
		|	ПроводкиОстаткиИОбороты.Подразделение КАК Подразделение,
		|	ПроводкиОстаткиИОбороты.СуммаНачальныйОстатокДт,
		|	ПроводкиОстаткиИОбороты.СуммаОборотДт,
		|	ПроводкиОстаткиИОбороты.СуммаОборотКт,
		|	ПроводкиОстаткиИОбороты.КоличествоНачальныйОстатокДт,
		|	ПроводкиОстаткиИОбороты.КоличествоОборотДт,
		|	ПроводкиОстаткиИОбороты.КоличествоОборотКт
		|ИЗ
		|	РегистрБухгалтерии.Проводки.ОстаткиИОбороты(&Дата1, &Дата2,,,,
		|		&ВидыСубконто, Организация = &Организация) КАК ПроводкиОстаткиИОбороты
		|ИТОГИ
		|ПО
		|	Склад,
		|	Подразделение,
		|	Номенклатура";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаСклад.Следующий() Цикл

		ВыборкаПодразделение = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПодразделение.Следующий() Цикл

			ВыборкаНоменклатура = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаНоменклатура.Следующий() Цикл

				ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();

				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

					СреднеВзвешеннаяЦена = (ВыборкаДетальныеЗаписи.СуммаНачальныйОстатокДт
						+ ВыборкаДетальныеЗаписи.СуммаОборотДт)
						/ (ВыборкаДетальныеЗаписи.КоличествоНачальныйОстатокДт
						+ ВыборкаДетальныеЗаписи.КоличествоОборотДт);

					РазницаВСтоимости = ВыборкаДетальныеЗаписи.КоличествоОборотДт
						* СреднеВзвешеннаяЦена - ВыборкаДетальныеЗаписи.СуммаОборотКт;

					Если (ВыборкаДетальныеЗаписи.КоличествоОборотКт > 0
							И РазницаВСтоимости <> 0) Тогда

						Движение = Движения.Проводки.Добавить();
						Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;
						Движение.СчетКт = ПланыСчетов.Бухгалтерский.Товары;

						БухгалтерияСервер.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Номенклатура", ВыборкаДетальныеЗаписи.Номенклатура);
						БухгалтерияСервер.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Склады", ВыборкаДетальныеЗаписи.Склад);

						Движение.КоличествоКт = 0;

						Движение.Период = Дата;
						Движение.Сумма = РазницаВСтоимости;
						Движение.Организация = Организация;
						Движение.Содержание = "Корректировка стоимости номенклатуры";

						БухгалтерияСервер.УстановитьПодразделенияПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Подразделение);

					КонецЕсли;

				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаПроведенияНетУчетаПоСкладам(Отказ, РежимПроведения)

	Движения.Проводки.Записывать = Истина;

	Запрос = Новый Запрос;

	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);

	Запрос.Текст = "ВЫБРАТЬ
		|	ПроводкиОстаткиИОбороты.Субконто1 КАК Номенклатура,
		|	ПроводкиОстаткиИОбороты.Счет,
		|	ПроводкиОстаткиИОбороты.Подразделение КАК Подразделение,
		|	ПроводкиОстаткиИОбороты.СуммаНачальныйОстатокДт,
		|	ПроводкиОстаткиИОбороты.СуммаОборотДт,
		|	ПроводкиОстаткиИОбороты.СуммаОборотКт,
		|	ПроводкиОстаткиИОбороты.КоличествоНачальныйОстатокДт,
		|	ПроводкиОстаткиИОбороты.КоличествоОборотДт,
		|	ПроводкиОстаткиИОбороты.КоличествоОборотКт
		|ИЗ
		|	РегистрБухгалтерии.Проводки.ОстаткиИОбороты(&Дата1, &Дата2,,,,
		|		&ВидыСубконто, Организация = &Организация) КАК ПроводкиОстаткиИОбороты
		|ИТОГИ
		|ПО
		|	Подразделение,
		|	Номенклатура";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПодразделение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПодразделение.Следующий() Цикл

		ВыборкаНоменклатура = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаНоменклатура.Следующий() Цикл

			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();

			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

				СреднеВзвешеннаяЦена = (ВыборкаДетальныеЗаписи.СуммаНачальныйОстатокДт
					+ ВыборкаДетальныеЗаписи.СуммаОборотДт)
					/ (ВыборкаДетальныеЗаписи.КоличествоНачальныйОстатокДт
					+ ВыборкаДетальныеЗаписи.КоличествоОборотДт);

				РазницаВСтоимости = ВыборкаДетальныеЗаписи.КоличествоОборотДт
					* СреднеВзвешеннаяЦена - ВыборкаДетальныеЗаписи.СуммаОборотКт;

				Если (ВыборкаДетальныеЗаписи.КоличествоОборотКт > 0
						И РазницаВСтоимости <> 0) Тогда

					Движение = Движения.Проводки.Добавить();
					Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;
					Движение.СчетКт = ПланыСчетов.Бухгалтерский.Товары;

					БухгалтерияСервер.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Номенклатура", ВыборкаДетальныеЗаписи.Номенклатура);

					Движение.КоличествоКт = 0;

					Движение.Период = Дата;
					Движение.Сумма = РазницаВСтоимости;
					Движение.Организация = Организация;
					Движение.Содержание = "Корректировка стоимости номенклатуры";

					БухгалтерияСервер.УстановитьПодразделенияПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, ВыборкаДетальныеЗаписи.Подразделение);

				КонецЕсли;

			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры
