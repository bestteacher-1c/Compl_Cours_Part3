Процедура УстановитьПодразделениеПроводки(Проводка, Подразделение, ДтКт)

	Если Проводка["Счет" + ДтКт].УчетПоПодразделениям = Истина Тогда

		Проводка["Подразделение" + ДтКт] = Подразделение;

	КонецЕсли;

КонецПроцедуры

Процедура УстановитьПодразделенияПроводки(Проводка, ПодразделениеДт,
		ПодразделениеКт) Экспорт

	УстановитьПодразделениеПроводки(Проводка, ПодразделениеДт, "Дт");
	УстановитьПодразделениеПроводки(Проводка, ПодразделениеКт, "Кт");

КонецПроцедуры

Процедура УстановитьСубконто(Счет, Субконто, ИмяСубконто,
		ЗначениеСубконто) экспорт

	Если ЗначениеЗаполнено(Счет) = Ложь
			Или ЗначениеЗаполнено(ИмяСубконто) = Ложь Тогда

		Возврат;

	КонецЕсли;

	ДанныеСчета = БухгалтерияСерверПовторно.ПолучитьСвойствоСчета(Счет);

	Если ТипЗнч(ИмяСубконто) = Тип("Число") Тогда

		Если ИмяСубконто > ДанныеСчета.КоличествоСубконто Тогда

			Возврат;

		КонецЕсли;

		ВидСубконтоСсылка = ДанныеСчета["ВидСубконто" + ИмяСубконто];

	Иначе

		ВидСубконтоСсылка = ПланыВидовХарактеристик.ВидыСубконто[ИмяСубконто];

		Если ДанныеСчета.ВидСубконто1 <> ВидСубконтоСсылка
				И ДанныеСчета.ВидСубконто2 <> ВидСубконтоСсылка Тогда

			Возврат;

		КонецЕсли;

	КонецЕсли;

	Если ВидСубконтоСсылка.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто)) = Истина Тогда

		Субконто.Вставить(ВидСубконтоСсылка, ЗначениеСубконто);

	КонецЕсли;

КонецПроцедуры

Функция РассчитатьЭквивалент(ВалютнаяСумма, Валюта, Дата) Экспорт

	Отбор = Новый Структура("Валюта", Валюта);
	КурсИКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Отбор);
	Курс = КурсИКратность.Курс;

	Если (Курс = 0) Тогда
		Курс = 1;
	КонецЕсли;

	Кратность = КурсИКратность.Кратность;

	Если (Кратность = 0) Тогда
		Кратность = 1;
	КонецЕсли;

	Возврат ВалютнаяСумма * Курс / Кратность;
	
КонецФункции

Функция ПолучитьКроссКурсВалют(Валюта1, Валюта2, Период) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта В (&Валюта1, &Валюта2)) КАК КурсыВалютСрезПоследних";

	Запрос.УстановитьПараметр("Валюта1", Валюта1);
	Запрос.УстановитьПараметр("Валюта2", Валюта2);
	Запрос.УстановитьПараметр("Период", Период);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Если (ВыборкаДетальныеЗаписи.Валюта = Валюта1) Тогда

			КурсВалюты1 = ?(ВыборкаДетальныеЗаписи.Курс = 0, 1, ВыборкаДетальныеЗаписи.Курс);
			КратностьВалюты1 = ?(ВыборкаДетальныеЗаписи.Кратность = 0, 1, ВыборкаДетальныеЗаписи.Кратность);

		ИначеЕсли (ВыборкаДетальныеЗаписи.Валюта = Валюта2) Тогда

			КурсВалюты2 = ?(ВыборкаДетальныеЗаписи.Курс = 0, 1, ВыборкаДетальныеЗаписи.Курс);
			КратностьВалюты2 = ?(ВыборкаДетальныеЗаписи.Кратность = 0, 1, ВыборкаДетальныеЗаписи.Кратность);

		КонецЕсли;

	КонецЦикла;

	КроссКурс = КурсВалюты1 / КратностьВалюты1 / КурсВалюты2 * КратностьВалюты2;

	Возврат КроссКурс;

КонецФункции