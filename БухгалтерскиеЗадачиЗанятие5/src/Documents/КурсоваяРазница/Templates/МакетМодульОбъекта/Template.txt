Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Блокировка = Новый БлокировкаДанных;
	
	ЧтоБлокируем = Блокировка.Добавить("РегистрБухгалтерии.Проводки");
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Счета.Ссылка
		|ИЗ
		|	ПланСчетов.Счета КАК Счета
		|ГДЕ
		|	Счета.НеИспользоватьВПроводках = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЧтоБлокируем.ИсточникДанных = РезультатЗапроса;
	ЧтоБлокируем.ИспользоватьИзИсточникаДанных("Счет","Ссылка");
	
	Блокировка.Заблокировать();
	
	
	Движения.Проводки.Записывать = Истина;
	Движения.Записать();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроводкиОстатки.Счет,
		|	ПроводкиОстатки.Субконто1,
		|	ПроводкиОстатки.Субконто2,
		|	ПроводкиОстатки.Организация,
		|	ПроводкиОстатки.Подразделение,
		|	ПроводкиОстатки.Валюта,
		|	ПроводкиОстатки.СуммаОстатокДт,
		|	ПроводкиОстатки.СуммаОстатокКт,
		|	ПроводкиОстатки.СуммаВалютыОстатокДт,
		|	ПроводкиОстатки.СуммаВалютыОстатокКт
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрБухгалтерии.Проводки.Остатки(&Период, Счет.Валютный = ИСТИНА,,
		|		Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
		|	И Валюта <> &ВалютаУчета) КАК ПроводкиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта,
		|	КурсыВалютСрезПоследних.Курс
		|ПОМЕСТИТЬ ВТКурс
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта В
		|		(ВЫБРАТЬ
		|			ВТОстатки.Валюта
		|		ИЗ
		|			ВТОстатки КАК ВТОстатки)) КАК КурсыВалютСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОстатки.Счет,
		|	ВТОстатки.Субконто1,
		|	ВТОстатки.Субконто2,
		|	ВТОстатки.Организация,
		|	ВТОстатки.Подразделение,
		|	ВТОстатки.Валюта,
		|	ВТОстатки.СуммаОстатокДт,
		|	ВТОстатки.СуммаОстатокКт,
		|	ВТОстатки.СуммаВалютыОстатокДт,
		|	ВТОстатки.СуммаВалютыОстатокКт,
		|	ЕСТЬNULL(ВТКурс.Курс, 1) КАК Курс
		|ИЗ
		|	ВТОстатки КАК ВТОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурс КАК ВТКурс
		|		ПО ВТОстатки.Валюта = ВТКурс.Валюта";
	
	Запрос.УстановитьПараметр("Период", МоментВремени());
	Запрос.УстановитьПараметр("ВалютаУчета", Справочники.Валюты.РубльРФ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
		ОстатокПоНовомуКурсуДт = ВыборкаДетальныеЗаписи.СуммаВалютыОстатокДт * ВыборкаДетальныеЗаписи.Курс;
		ОстатокПоНовомуКурсуРублиКт = ВыборкаДетальныеЗаписи.СуммаВалютыОстатокКт * ВыборкаДетальныеЗаписи.Курс;
		
		КурсоваяРазницаДт = ВыборкаДетальныеЗаписи.СуммаОстатокДт - ОстатокПоНовомуКурсуДт;
		КурсоваяРазницаКт = ВыборкаДетальныеЗаписи.СуммаОстатокКт - ОстатокПоНовомуКурсуРублиКт;

		Если (ВыборкаДетальныеЗаписи.СуммаВалютыОстатокДт > 0 и КурсоваяРазницаДт > 0) 
		 Или (ВыборкаДетальныеЗаписи.СуммаВалютыОстатокКт >0 и КурсоваяРазницаКт < 0)  Тогда
	
			КурсоваяРазница = ?(КурсоваяРазницаДт > 0, КурсоваяРазницаДт, -1*КурсоваяРазницаКт);
			ОтразитьКурсовуюРазницуПоДебетуСчета(ВыборкаДетальныеЗаписи, КурсоваяРазница);		

		ИначеЕсли (ВыборкаДетальныеЗаписи.СуммаВалютыОстатокКт > 0 и КурсоваяРазницаКт > 0) 
			  Или (ВыборкаДетальныеЗаписи.СуммаВалютыОстатокДт >0 и КурсоваяРазницаДт < 0) Тогда
		
			КурсоваяРазница = ?(КурсоваяРазницаКт > 0, КурсоваяРазницаКт, -1*КурсоваяРазницаДт);
			ОтразитьКурсовуюРазницуПоКредитуСчета(КурсоваяРазница, ВыборкаДетальныеЗаписи);

		КонецЕсли;
		
	КонецЦикла;	

	Движения.Проводки.Записывать = Истина;
	
	
	
КонецПроцедуры

Процедура ОтразитьКурсовуюРазницуПоДебетуСчета(ВыборкаДетальныеЗаписи, Знач КурсоваяРазница)

	Движение = Движения.Проводки.Добавить();

	Движение.Период = Дата;
	Движение.Организация = ВыборкаДетальныеЗаписи.Организация;
	Движение.СодержаниеПроводки = "Курсовые разницы";
	Движение.Сумма = КурсоваяРазница;
	
	//Дт

	Движение.СчетДт = ВыборкаДетальныеЗаписи.Счет;

	БухгалтерияНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, "Дт");

	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 1, ВыборкаДетальныеЗаписи.Субконто1, "Дт");
	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 2, ВыборкаДетальныеЗаписи.Субконто2, "Дт");

	БухгалтерияНаСервере.ЗаполнитьВалютуСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Валюта, 0, "Дт");
	
	//Кт

	Движение.СчетКт = ПланыСчетов.Счета.РасходыДоходы;

	БухгалтерияНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, "Кт");

	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 1, ВыборкаДетальныеЗаписи.Субконто1, "Кт");
	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 2, ВыборкаДетальныеЗаписи.Субконто2, "Кт");

	БухгалтерияНаСервере.ЗаполнитьВалютуСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Валюта, 0, "Кт");
КонецПроцедуры

Процедура ОтразитьКурсовуюРазницуПоКредитуСчета(Знач КурсоваяРазница, ВыборкаДетальныеЗаписи)

	Движение = Движения.Проводки.Добавить();

	Движение.Период = Дата;
	Движение.Организация = ВыборкаДетальныеЗаписи.Организация;
	Движение.СодержаниеПроводки = "Курсовые разницы";
	Движение.Сумма = КурсоваяРазница;
	
	//Дт

	Движение.СчетДт = ПланыСчетов.Счета.РасходыДоходы;

	БухгалтерияНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, "Дт");

	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 1, ВыборкаДетальныеЗаписи.Субконто1, "Дт");
	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 2, ВыборкаДетальныеЗаписи.Субконто2, "Дт");

	БухгалтерияНаСервере.ЗаполнитьВалютуСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Валюта, 0, "Дт");
	
	//Кт

	Движение.СчетКт = ВыборкаДетальныеЗаписи.Счет;

	БухгалтерияНаСервере.ЗаполнитьПодразделениеСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Подразделение, "Кт");

	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 1, ВыборкаДетальныеЗаписи.Субконто1, "Кт");
	БухгалтерияНаСервере.ЗаполнитьСубконтоСтороныПроводки(Движение, 2, ВыборкаДетальныеЗаписи.Субконто2, "Кт");

	БухгалтерияНаСервере.ЗаполнитьВалютуСтороныПроводки(Движение, ВыборкаДетальныеЗаписи.Валюта, 0, "Кт");
КонецПроцедуры