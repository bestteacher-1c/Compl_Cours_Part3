
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьВидимостьЭлементовФормы();
	
	КорСчетПриИзмененииНаСервере();
	МестоХраненияДенежныхСредствПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	УстановитьВидимостьЭлементовФормы();
	
	КорСчетПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("ПФО_Организация", Объект.Организация);
	
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры // УстановитьВидимостьЭлементовФормы()


&НаСервере
Процедура МестоХраненияДенежныхСредствПриИзмененииНаСервере()
	
	ПараметрыФО = Новый Структура("ПФО_Организация", Объект.Организация);
	ВалютныйУчетПоОрганизации = ПолучитьФункциональнуюОпцию("ВалютныйУчет", ПараметрыФО);

	Если (ВалютныйУчетПоОрганизации = Истина) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	МестаХраненияДенежныхСредств.СчетУчета
			|ИЗ
			|	Справочник.МестаХраненияДенежныхСредств КАК МестаХраненияДенежныхСредств
			|ГДЕ
			|	МестаХраненияДенежныхСредств.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.МестоХраненияДенежныхСредств);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий(); 

		ДанныеСчета = НаСервереМожноВызватьСКлиента.ПолучитьСвойствоСчета(ВыборкаДетальныеЗаписи.СчетУчета);

		Если (ДанныеСчета.Валютный = Истина) Тогда
			Элементы.Валюта.Видимость = Истина
		Иначе
			Элементы.Валюта.Видимость = Ложь
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МестоХраненияДенежныхСредствПриИзменении(Элемент)

	МестоХраненияДенежныхСредствПриИзмененииНаСервере();

КонецПроцедуры


&НаСервере
Процедура КорСчетПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.КорСчет) = Ложь Тогда
		Возврат;
	КонецЕсли;

	СвойстваСчета = НаСервереМожноВызватьСКлиента.ПолучитьСвойствоСчета(Объект.КорСчет);
	МаксКоличествоСубконто = СвойстваСчета.МаксКоличествоСубконто;

	Для Индекс = 1 По МаксКоличествоСубконто Цикл

		Если (Индекс <= СвойстваСчета.КоличествоСубконто) Тогда

			Элементы["КорСубконто" + Индекс].Доступность = Истина;
			Элементы["КорСубконто" + Индекс].Заголовок = СвойстваСчета["ВидСубконто"
				+ Индекс + "Наименование"];
			Элементы["КорСубконто" + Индекс].ВыбиратьТип = Ложь;

			Объект["КорСубконто" + Индекс] = СвойстваСчета["ВидСубконто" + Индекс
				+ "ТипЗначения"].ПривестиЗначение(Объект["КорСубконто" + Индекс]);

		Иначе

			Элементы["КорСубконто" + Индекс].Доступность = Ложь;
			Элементы["КорСубконто" + Индекс].Заголовок = "Не определено";

			Объект["КорСубконто" + Индекс] = Неопределено;

		КонецЕсли;

	КонецЦикла;

	КорСубконто1ПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура КорСчетПриИзменении(Элемент)
	КорСчетПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КорСубконто1ПриИзменении(Элемент)
	КорСубконто1ПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КорСубконто1ПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.КорСчет) = Ложь Тогда
		Возврат;
	КонецЕсли;

	СвойстваСчета = НаСервереМожноВызватьСКлиента.ПолучитьСвойствоСчета(Объект.КорСчет);
	
	Массив = Новый Массив;

	Если ТипЗнч(Объект.КорСубконто2) = Тип("СправочникСсылка.ЗначенияПользовательскихСубконто") Тогда
		
		Объект.КорСубконто2 = Справочники.ЗначенияПользовательскихСубконто.ПустаяСсылка();
		
		Массив.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СвойстваСчета.ВидСубконто2));

	ИначеЕсли ТипЗнч(Объект.КорСубконто2) = Тип("СправочникСсылка.Договоры") 
		И ТипЗнч(Объект.КорСубконто1) = Тип("СправочникСсылка.Контрагенты")  Тогда

		Если Не Объект.КорСубконто2.Владелец = Объект.КорСубконто1 или
			Не Объект.КорСубконто2.Организация = Объект.Организация Тогда
		
			Объект.КорСубконто2 = Справочники.Договоры.ПустаяСсылка();
		
		КонецЕсли;


		Массив.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Объект.КорСубконто1));
		Массив.Добавить(Новый ПараметрВыбора("Отбор.Организация", Объект.Организация));

	КонецЕсли;

	Элементы.КорСубконто2.ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры





