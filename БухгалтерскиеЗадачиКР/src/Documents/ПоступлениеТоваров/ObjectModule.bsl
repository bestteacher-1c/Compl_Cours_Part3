Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПолучитьФункциональнуюОпцию("УчетПоПодразделениям"
		, Новый Структура("ПФО_Организация", Организация)) = Ложь Тогда
	
		  Индекс = ПроверяемыеРеквизиты.Найти("Подразделение");
			ПроверяемыеРеквизиты.Удалить(Индекс);
			
	
	КонецЕсли;                      
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Проводки.Записывать = Истина;
	
	КроссКурс = СерверныйСВызовом.ПересчитатьИзВалютыВВалютуНаСервере(Валюта,,1,Дата);
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		
		Движение = Движения.Проводки.Добавить();
		
		//--Общие для проводки -----
		
		Движение.Период = Дата;
		Движение.Сумма = ТекСтрокаТовары.Сумма * КроссКурс;
		Движение.СодержаниеПроводки = "Поступили ТМЦ";
		Движение.Организация = Организация;
		
		//--Дебет -----
		Движение.СчетДт = ТекСтрокаТовары.СчетУчета;
		//Движение.ПодразделениеДт = Подразделение;
		Серверный.ЗаполнитьПодразделениеСтороныПроводки(Движение,Подразделение,"Дт");
		
		Серверный.ЗаполнитьСубконтоСтороныПроводки(Движение,"Номенклатура", ТекСтрокаТовары.Номенклатура, "Дт");
		Серверный.ЗаполнитьСубконтоСтороныПроводки(Движение,"Склады", Склад, "Дт");
		Серверный.ЗаполнитьСубконтоСтороныПроводки(Движение,"Контрагенты", Контрагент, "Дт");
		
		Серверный.ЗаполнитьКоличествоСтороныПроводки(Движение, ТекСтрокаТовары.Количество, "Дт");
		//--Кредит ----
		Если Не ТекСтрокаТовары.СчетУчета.Забалансовый Тогда
		
			Движение.СчетКт = ПланыСчетов.Счета.Поставщики;
			Серверный.ЗаполнитьПодразделениеСтороныПроводки(Движение,Подразделение,"Кт");
			
			Серверный.ЗаполнитьСубконтоСтороныПроводки(Движение,"Контрагенты", Контрагент, "Кт");
			Серверный.ЗаполнитьСубконтоСтороныПроводки(Движение,"Договоры", Договор, "Кт");
			
			Серверный.ЗаполнитьКоличествоСтороныПроводки(Движение, ТекСтрокаТовары.Количество, "Кт");
			
			Серверный.ЗаполнитьВалютуСтороныПроводки(Движение, Валюта, ТекСтрокаТовары.Сумма, "Кт");
			
		КонецЕсли; 
		
		
	КонецЦикла;

КонецПроцедуры




